group 'net.bndy'
version '1.1'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    maven { url "https://oss.sonatype.org/content/groups/staging" }
    mavenCentral()
}

dependencies {
    compile group: 'net.bndy', name: 'ftsi', version: '1.6'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.0.0.RELEASE'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task copyDocs(type: Copy, dependsOn: javadocJar) {
    from 'build/docs'
    into 'docs'
}
javadocJar.finalizedBy(copyDocs)

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Spring Boot FTSI Starter'
                packaging 'jar'

                // optionally artifactId can be defined here
                description 'Spring Boot Starter for Full Text Search Interface(ftsi)'
                url 'https://github.com/bndynet/spring-boot-starter-ftsi'

                organization {
                    name 'BNDY-NET'
                    url 'http://bndy.net'
                }

                scm {
                    url 'https://github.com/bndynet/spring-boot-starter-ftsi'
                    connection 'scm:git:git://github.com/bndynet/spring-boot-starter-ftsi.git'
                    developerConnection 'scm:git:git@github.com:bndynet/spring-boot-starter-ftsi.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'bndynet'
                        name 'Bendy Zhang'
                        email 'zb@bndy.net'
                        url 'http://bndy.net'
                    }
                }
            }
        }
    }
}